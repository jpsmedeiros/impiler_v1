num = @{ int ~ ("." ~ digit*)? ~ (^"e" ~ int)? }
    int = { ("+" | "-")? ~ digit+ }
    digit = { '0'..'9' }

boolean = { "true" | "false" }

arithop = _{ add | subtract | multiply | divide }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }


boolop = _{ equal | and | or }
    equal = { "=" }
    and   = { "/\u{005C}" }
    or    = { "\u{005C}/"}

    
iop = _{ greater_equal | less_equal | greater_than | less_than  }
    greater_than  = { ">" }
    greater_equal = { ">=" }
    less_than     = { "<" }
    less_equal    = { "<=" } 

neg   = { "~" }

exp = _{ bexp | aexp }
aexp = { aterm ~ (arithop ~ aterm)* }

bexp = { bterm ~ (boolop ~ bterm)* | aexp ~ iop ~ aexp }

aterm = _{ num | "(" ~ aexp ~ ")" | id}
bterm = _{ boolean | "(" ~ bexp ~ ")" | neg ~ bterm }

identifier_char = _{ 'a'..'z' | 'A'..'Z' | '0'..'9' | "_" }
id      =  {
    !("true" | "false" ) ~ ('a'..'z' | 'A'..'Z') ~ identifier_char* |
    "_" ~ identifier_char+
}

cmd = { cmdterm ~ ";" ~ cmd | cmdterm}

cmdterm = _{ assign_cmd |  while_cmd}

assign_cmd = { id ~ ":=" ~ exp }
while_cmd = { "while" ~ "(" ~ bexp ~ ")" ~ "do" ~ "{" ~ cmd ~ "}" }

impiler = _{ soi ~ (cmd | exp) ~ eoi }

whitespace = _{ " " | "\t" }